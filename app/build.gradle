import java.text.SimpleDateFormat

def buildVersionNameMajorMinorPatchString() {
    return "0.1.4";
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    repositories {
        jcenter()
        mavenCentral()
        //Backup http source for dependencies just in case certificates get updated and builds start failing again
        jcenter {
            url "http://jcenter.bintray.com/"
        }
    }

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.chrisprime.primestationonecontrol"
        minSdkVersion 9
        targetSdkVersion 22
        versionName genVersionName()
        versionCode gitVersion()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    lintOptions {
        abortOnError true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

def gitVersion() {
    def process = "git rev-list HEAD --first-parent --count".execute()
    def gitVersionInteger = process.text.toInteger()
    println "gitVersionInteger (buildVersionCode) = " + gitVersionInteger
    return gitVersionInteger
}

def buildTime() {
    def df = new SimpleDateFormat("yyyyMMddhhmmSS")
    return df.format(new Date())
}

def genVersionName() {
    def versionName = buildVersionNameMajorMinorPatchString() + "." + buildTime()
    println "buildVersionName = " + versionName
    return versionName;
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    configurations.all {
        // Enforce latest android support annotations for all dependencies
        resolutionStrategy.force 'com.android.support:support-annotations:22.2.1'
    }

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxandroid:0.25.0'
    compile 'com.jcraft:jsch:0.1.53'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'

    compile 'com.google.code.gson:gson:2.3.1'

    // Needed for dagger 2
    compile 'com.google.dagger:dagger:2.0.1'
    apt "com.google.dagger:dagger-compiler:2.0.1"

    // Needed specifically for annotations in Java 1.6
    compile 'org.glassfish:javax.annotation:10.0-b28'

    // http://parceler.org/
    compile 'org.parceler:parceler-api:1.0.1'
    apt 'org.parceler:parceler:1.0.1'

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support.test:runner:0.3'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:0.3'
}

String java8 = getJavaVersion(8)
String java7 = getJavaVersion(7)

String getJavaVersion(Integer v) {
    def retrievedJavaVersionHome;
    retrievedJavaVersionHome = System.getenv("JAVA$v" + "_HOME")
    if (retrievedJavaVersionHome == null || retrievedJavaVersionHome.size() < 1) {
        def sout = new StringBuffer()
        def proc = "/usr/libexec/java_home -v 1.$v".execute()
        proc.consumeProcessOutput(sout, new StringBuffer())
        proc.waitForOrKill(1000)
        retrievedJavaVersionHome = sout.toString().replace("\n", "").replace("\r", "")
    }
    println "getJavaVersion($v" + ") has retrieved $retrievedJavaVersionHome"
    return retrievedJavaVersionHome
}

retrolambda {
    jdk java8
    oldJdk java7
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}

// Get the path to ADB.  Required when running tests directly from Android Studio.
// Source: http://stackoverflow.com/a/26771087/112705
def adb = android.getAdbExe().toString()

// Source: http://stackoverflow.com/q/29908110/112705
afterEvaluate {
    task grantAnimationPermissionDev(type: Exec, dependsOn: 'installDebug') {
        def grantAnimationPermissionCommand = "$adb shell pm grant $android.defaultConfig.applicationId android.permission.SET_ANIMATION_SCALE"
        println grantAnimationPermissionCommand
        commandLine grantAnimationPermissionCommand.split(' ')
    }

    task grantAnimationPermissionProduction(type: Exec, dependsOn: 'installRelease') {
        def grantAnimationPermissionCommand = "$adb shell pm grant $android.defaultConfig.applicationId android.permission.SET_ANIMATION_SCALE"
        println grantAnimationPermissionCommand
        commandLine grantAnimationPermissionCommand.split(' ')
    }

    task unlockEmulatorDev(type: Exec, dependsOn: 'assembleDebugAndroidTest') {
        def unlockEmulatorCommand = "$adb shell input keyevent 82"
        println unlockEmulatorCommand
        commandLine unlockEmulatorCommand.split(' ')
    }

    task unlockEmulatorProduction(type: Exec, dependsOn: 'assembleReleaseAndroidTest') {
        def unlockEmulatorCommand = "$adb shell input keyevent 82"
        println unlockEmulatorCommand
        commandLine unlockEmulatorCommand.split(' ')
    }

    // When launching individual tests from Android Studio, it seems that only the assemble tasks
    // get called directly, not the install* versions
    tasks.each { task ->
        if (task.name.startsWith('assembleDebugAndroidTest')) {
            task.dependsOn grantAnimationPermissionDev
        } else if (task.name.startsWith('assembleReleaseAndroidTest')) {
            task.dependsOn grantAnimationPermissionProduction
        } else if (task.name.startsWith('connectedAndroidTestDebug')) {
            task.dependsOn unlockEmulatorDev
        } else if (task.name.startsWith('connectedAndroidTestRelease')) {
            task.dependsOn unlockEmulatorProduction
        }
    }
}

